plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.1'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.apex'
version = '2.0.0'
description = 'Apex Trading Bot - High Frequency Trading System'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	// Spring Boot Starters
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-websocket'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.flywaydb:flyway-core'

	// JWT Dependencies
	implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.3'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.3'

	// BCrypt for password hashing
	implementation 'org.springframework.security:spring-security-crypto'

	// Notifications (Email & WhatsApp)
	implementation 'org.springframework.boot:spring-boot-starter-mail'
	implementation 'com.twilio.sdk:twilio:10.1.0'

	// Resilience & Monitoring
	implementation 'io.github.resilience4j:resilience4j-spring-boot3:2.1.0'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-hibernate6'

	// Fyers API & HTTP
	implementation 'com.squareup.okhttp3:okhttp:4.12.0'
	implementation 'com.google.code.gson:gson'

	// Math & Indicators
	implementation 'org.apache.commons:commons-math3:3.6.1'

	// Database (H2 for development, PostgreSQL for production)
	implementation 'org.postgresql:postgresql'
	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'org.postgresql:postgresql'

	// lombokk
	compileOnly 'org.projectlombok:lombok:1.18.30'
	annotationProcessor 'org.projectlombok:lombok:1.18.30'

	// Testing
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.boot:spring-boot-testcontainers'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:postgresql'
	testImplementation 'com.github.tomakehurst:wiremock-jre8:2.35.0'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

// Custom task to run with dev profile
task bootRunDev(type: JavaExec) {
	group = 'Application'
	description = 'Run with development profile'
	classpath = sourceSets.main.runtimeClasspath
	mainClass = 'com.apex.backend.BackendApplication'
	args = ['--spring.profiles.active=dev']

	javaLauncher = javaToolchains.launcherFor {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

// Custom task to run with production profile
task bootRunProd(type: JavaExec) {
	group = 'Application'
	description = 'Run with production profile'
	classpath = sourceSets.main.runtimeClasspath
	mainClass = 'com.apex.backend.BackendApplication'
	args = ['--spring.profiles.active=prod']

	javaLauncher = javaToolchains.launcherFor {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

dependencies {
	implementation 'org.flywaydb:flyway-database-postgresql'
}
